plugins {
    id 'java'
    id 'maven-publish'
}

group = 'dev.puzzleshq.solarflare'

def ref = System.getenv("GITHUB_REF")
version = ((ref == null) ? "refs/tags/0.0.0-alpha" : ref).replaceAll("refs/tags/", "")

String mavenUrl = System.getenv("MAVEN_URL")

String envRepo = System.getenv("MAVEN_REPO")
String mavenRepo = envRepo != null ? envRepo : "releases"

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed"
    }
}

publishing {
    repositories {
        maven {
            name = "PuzzleHQsMaven"
            url = mavenUrl + "/" +mavenRepo
            credentials {
                username = System.getenv("MAVEN_NAME")
                password = System.getenv("MAVEN_SECRET")
            }
            authentication {
                basic(BasicAuthentication)
            }
        }
        mavenLocal()
    }

    publications {
        maven(MavenPublication) {
            groupId = group
            artifactId = 'corona-archive-util'

            from components.java

            pom {
                name = 'corona-archive-util'
                description = 'An implementation of CoronaLab\'s ".car" archive format in java.'
                url = 'https://github.com/PuzzlesHQ/corona-archive-util.git'

                licenses {
                    license {
                        name = "MIT LICENSE"
                        url = "https://opensource.org/license/mit"
                        distribution = "MIT"
                        comments = "This project includes MIT-licensed components."
                    }
                }

                developers {
                    developer {
                        id = 'mrzombii'
                        name = 'Mr Zombii'
                        email = 'thezombiigames@gmail.com'
                    }
                    developer {
                        id = 'crabking'
                        name = 'CrabKing'
                    }
                }
                scm {
                    connection = 'https://github.com/PuzzlesHQ/corona-archive-util.git'
                    developerConnection = 'https://github.com/PuzzlesHQ/corona-archive-util.git'
                    url = 'https://github.com/PuzzlesHQ/corona-archive-util.git'
                }
            }
        }
    }
}

if (ref != null) {
    signing {
        useGpgCmd()
        sign(publishing.publications[0])
    }
}

wrapper {
    gradleVersion = "8.14"
    distributionType = Wrapper.DistributionType.ALL
}